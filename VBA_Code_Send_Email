Sub Mail_workbook_Outlook()

    'Working in Excel 2000-2016
    'This example sends the last saved version of the Activeworkbook
    
    Dim sRecipients As String   '   stores recipients list
    Dim sCC As String           '   stores CC list
    Dim sSubject As String      '   stores subject
    Dim rngTable As Range
    
    Dim wdDoc As Word.Document
    
    Dim OutApp As Object
    Dim OutMail As Object
    Dim oTestOutApp As Object
        
    Set OutApp = CreateObject("Outlook.Application")
    Set OutMail = OutApp.CreateItem(0)
    
    '   checki if outlook is , if not, open outlook
    On Error Resume Next
        Set oTestOutApp = GetObject(, "Outlook.Application")
    On Error GoTo 0
    
    If oTestOutApp Is Nothing Then
        Shell ("OUTLOOK")
    End If
    
    '   set word as html editor
    Set wdDoc = OutMail.GetInspector.WordEditor
    
    ' get table range, here we use a named range called "Stock_Report_Table"
    Set rngTable = ThisWorkbook.Sheets("Report").Range("Stock_Report_Table")
    
    '   Text is in HTML
    sBody = "<font size=""11pt"" face=""Calibri"" color=""black"">" _
        & "Hello," & "<br>" _
        & "<br>" _
        & "Please see attached for a summary of this report." & "<br>" _
        & "</font>" & RangetoHTML(rngTable)

    '   <br> is for line break
    sSignature = "<font size=8pt face=""Helvetica"" color=""red"">" _
        & "<br>" _
        & "<br>Thank you,<br>" _
        & "<b>CrowWarez</b><br>" _
        & "<a href=""https://www.instagram.com/antoniososa7997/"">Instagram</a> | " _
        & "<a href=""https://x.com/a_tsosa"">X</a> | " _
        & "<a href=""https://www.facebook.com/antonio.sosa"">Facebook</a></ | " _
        & "<a href=""https://www.youtube.com/@CrowWarez-l3m"">Youtube</a></font>"


    '   Edit recipients list or use a range in a worksheet
    sRecipients = _
    "Person who receives the report <email@domain.net>; "

    sCC = "email2@domain.net"

    '   subject has a dynamic date
    sSubject = "Sample Report - " & Format(Now, "Mmmm dd yyyy")
    
    On Error Resume Next
    With OutMail
        .To = sRecipients
        .CC = sCC
        .BCC = ""
        .Subject = sSubject
        .HTMLBody = sBody & sSignature
            'You can add other files also like this
            .Attachments.Add ActiveWorkbook.FullName
        .Attachments.Add sFilename
        .Attachments.Add sPDF
        .Display ' or use Send
    End With
    On Error GoTo 0
    
    Set OutMail = Nothing
    Set OutApp = Nothing
    
End Sub

Function RangetoHTML(rng As Range)

    '   Changed by Ron de Bruin 28-Oct-2006
    '   Working in Office 2000-2016
    '   This function converts an Excel table into HTML
    
    Dim fso As Object
    Dim ts As Object
    Dim TempFile As String
    Dim TempWB As Workbook
    
    TempFile = Environ$("temp") & "\" & Format(Now, "dd-mm-yy h-mm-ss") & ".htm"
    
    '   Copy the range and create a new workbook to past the data in
    rng.Copy
    Set TempWB = Workbooks.Add(1)
    With TempWB.Sheets(1)
        .Cells(1).PasteSpecial Paste:=8
        .Cells(1).PasteSpecial xlPasteValues, , False, False
        .Cells(1).PasteSpecial xlPasteFormats, , False, False
        .Cells(1).Select
        Application.CutCopyMode = False
        On Error Resume Next
        .DrawingObjects.Visible = True
        .DrawingObjects.Delete
        On Error GoTo 0
    End With
    
    '   Publish the sheet to a htm file
    With TempWB.PublishObjects.Add( _
         SourceType:=xlSourceRange, _
         Filename:=TempFile, _
         Sheet:=TempWB.Sheets(1).Name, _
         Source:=TempWB.Sheets(1).UsedRange.Address, _
         HtmlType:=xlHtmlStatic)
        .Publish (True)
    End With
    
    '   Read all data from the htm file into RangetoHTML
    Set fso = CreateObject("Scripting.FileSystemObject")
    Set ts = fso.GetFile(TempFile).OpenAsTextStream(1, -2)
    RangetoHTML = ts.readall
    ts.Close
    RangetoHTML = Replace(RangetoHTML, "align=center x:publishsource=", _
                          "align=left x:publishsource=")
    
    '   Close TempWB
    TempWB.Close savechanges:=False
    
    '   Delete the htm file we used in this function
    Kill TempFile
    
    Set ts = Nothing
    Set fso = Nothing
    Set TempWB = Nothing

End Function

